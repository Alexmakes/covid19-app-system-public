buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlin_version}"
    }
}

repositories {
    mavenCentral()
    jcenter()
}

apply plugin: 'kotlin'
apply plugin: 'jacoco'

compileKotlin.kotlinOptions.jvmTarget = "1.8"
compileTestKotlin.kotlinOptions.jvmTarget = "1.8"

jacocoTestReport {
    dependsOn test
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

task testSanity(type: Test) {
    description = 'Runs Sanity tests.'
    group = 'verification'

    useJUnitPlatform()

    filter {
        includeTestsMatching "uk.nhs.nhsx.sanity.lambdas.common.*"
        includeTestsMatching "uk.nhs.nhsx.sanity.lambdas.${System.getenv("TARGET_ENVIRONMENT")}.*"
    }

    reports.html.destination = file("$projectDir/../../out/reports")
    reports.junitXml.destination = file("$projectDir/../../out/reports")
}

dependencies {
    testImplementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:${kotlin_version}"

    testImplementation platform("org.http4k:http4k-bom:${http4k_version}")
    testImplementation "org.http4k:http4k-aws"
    testImplementation "org.http4k:http4k-core"
    testImplementation "org.http4k:http4k-format-jackson"
    testImplementation "org.http4k:http4k-testing-hamkrest"

    testImplementation platform("software.amazon.awssdk:bom:${aws_sdk_version}")
    testImplementation "software.amazon.awssdk:cloudfront"
    testImplementation "software.amazon.awssdk:kms"
    testImplementation "software.amazon.awssdk:s3"
    testImplementation "software.amazon.awssdk:sqs"
    testImplementation "software.amazon.awssdk:waf"

    testImplementation platform("dev.forkhandles:forkhandles-bom:${forkhandles_version}")
    testImplementation "dev.forkhandles:result4k"

    testImplementation platform("org.junit:junit-bom:${junit_version}")
    testImplementation "org.junit.jupiter:junit-jupiter-api"
    testImplementation "org.junit.jupiter:junit-jupiter-engine"
    testImplementation "org.junit.jupiter:junit-jupiter-params"

}
